;   ***************************************************************
;   * Copyright (C) 2018, Embed Inc (http://www.embedinc.com)     *
;   *                                                             *
;   * Permission to copy this file is granted as long as this     *
;   * copyright notice is included in its entirety at the         *
;   * beginning of the file, whether the file is copied in whole  *
;   * or in part and regardless of whether other information is   *
;   * added to the copy.                                          *
;   *                                                             *
;   * The contents of this file may be used in any way,           *
;   * commercial or otherwise.  This file is provided "as is",    *
;   * and Embed Inc makes no claims of suitability for a          *
;   * particular purpose nor assumes any liability resulting from *
;   * its use.                                                    *
;   ***************************************************************
;
/if [not [exist "fp32_flt48u_ins"]] then
  /const fp32_flt48u_ins bool = true

.section .code_fp32_flt48u, code
;*******************************************************************************
;
;   Subroutine FP32_FLT48U
;
;   Convert a 48 bit (3 16-bit word) unsigned fixed point value to IEEE 32 bit
;   floating point.
;
;   On entry:
;
;     W2:W1:W0  -  Fixed point input value.
;
;     W3  -  Number of fraction bits in the fixed point input value.  This can
;       be negative to indicate the binary point is some bits right of LSB of
;       the fixed point value.
;
;   On return:
;
;     W1:W0  -  IEEE 32 bit floating point value.
;
         glbsubd fp32_flt48u, regf2 | regf3 | regf4 | regf5 | regf6
;
;   Check for the special case of 0.  Later code requires at least one bit to be
;   set somewhere in the input number.
;
         cp      w0, #0
         cpb     w1, #0
         cpb     w2, #0
         bra     z, flt48u_zero ;is zero special case ?
;
;   Initialize the floating point exponent field value in W3.  This will be
;   initialized as if the highest 1 bit is in bit 7 of the high word (bit 7 of
;   W2) since this is the position that requires no shifting to make floating
;   point in W2:W1.
;
;   Higher values of W3 result in a lower exponent.  The W3 value of 39 means
;   that the bits in W2:W2 are already in the right position for the floating
;   point mantissa.  That would result in a exponent field value of 127.  We
;   therefore compute 166 - W3 to initialize the exponent field value in W3.
;
         mov     #166, w4    ;get value to subtract from
         sub     w4, w3, w3  ;init exponent field for mantissa in curr position
;
;   Find the most significant 1 bit.  W4 will be set to the number of bits in
;   from the left of the first 1 bit in W2.  The input value is shifted whole
;   words left if necessary to guarantee the highest 1 bit is in W2.  The zero
;   special case was handled earlier, so there is a 1 bit guaranteed to be
;   somewhere in W2:W1:W0.
;
;   W4 will be 1 if the highest bit is in the MSB of W2 and 16 if it is in the
;   LSB.
;
         ff1l    w2, w4      ;make number of bits in from left of first 1 bit
         bra     nc, flt48u_hw1 ;the first 1 is in the high word ?
         ;
         ;   The high word is 0.  Shift up one word and retry.
         ;
         mov     w1, w2      ;shift one word up
         mov     w0, w1
         mov     #0, w0
         sub     #16, w3     ;update the exponent to compensate

         ff1l    w2, w4      ;make number of bits in from left of first 1 bit
         bra     nc, flt48u_hw1 ;the first 1 is in the high word ?
         ;
         ;   The high word is 0.  Shift up one word and retry.
         ;
         mov     w1, w2      ;shift one word up
         mov     w0, w1
         mov     #0, w0
         sub     #16, w3     ;update the exponent to compensate

         ff1l    w2, w4      ;make number of bits in from left of first 1 bit

flt48u_hw1:                  ;highest 1 bit is W4 bits in from left of W2
;
;   The highest 1 bit is W4 bits in from the left end of W2.  W2:W1:W0 needs to
;   be shifted so that this is in bit 7, which would correspond to a W4 value of
;   9.  Therefore, if W4 is 9, the mantissa is already in place.  If it is less
;   than 9, the mantissa must be shifted right.  If it is more than 9 the
;   mantissa must be shifted left.
;
;   Now shift the mantissa in W2:W1:W0 so that the highest bit ends up in bit
;   7 of W2.  The final floating point mantissa will be taken from W2:W1.  The
;   contents of W0 does not matter except for the high bit, which will be used
;   to round the result.
;
         sub     #9, w4      ;make number of bits to shift left
         bra     z, flt48u_dshift ;already in place, done shifting ?
         bra     gt, flt48u_shleft ;need to shift left ?
         ;
         ;   Shift the mantissa -W4 bits right.  W4 is -1 to -8.
         ;
         ;   None of the original W0 bits matter.
         ;
         subr    w4, #0, w4  ;make number of bits to shift right
         add     w3, w4, w3  ;update the exponent to the shift
         subr    w4, #16, w5 ;bits to shift low parts left for next word

         sl      w2, w5, w6  ;part of W2 to shift into W1
         lsr     w2, w4, w2  ;make final W2
         sl      w1, w5, w0  ;shift the fragment from W1 into W0
         lsr     w1, w4, w1  ;move the W1 bits into place
         ior     w1, w6, w1  ;merge with contribution from W2
         jump    flt48u_dshift ;done shifting mantissa into place
         ;
         ;   Shift the mantissa W4 bits left.  W4 is 1 to 7.
         ;
flt48u_shleft:
         sub     w3, w4, w3  ;update the exponent to the shift
         subr    w4, #16, w5 ;bits to shift high parts right for next word

         sl      w2, w4, w2  ;move the W2 bits into place
         lsr     w1, w5, w6  ;make part of W1 to shift into W2
         ior     w2, w6, w2  ;assemble final W2

         sl      w1, w4, w1  ;move the W1 bits into place
         lsr     w0, w5, w0  ;make part of W0 to shift into W1
         ior     w1, w6, w1  ;assemble final W1

         sl      w0, w4, w0  ;move the w0 bits into place

flt48u_dshift:               ;done shifting the mantissa into place
;
;   The mantissa is in place in W2:W1.  Now round this value according to the
;   next lower bit, which is the high bit of W0.  The mantissa will also be
;   moved from W2:W1 into W1:W0 in the process.
;
         btst.c  w0, #15     ;copy the rounding bit into C
         addc    w1, #0, w0  ;round the mantissa and move it into W1:W0
         addc    w2, #0, w1
         ;
         ;   Check for the rounding caused a carry into the next higher mantissa
         ;   bit.  If so, the number needs to be shifted one more bit right.
         ;
         btss    w1, #8      ;next higher bit go set ?
         jump    flt48u_dround ;no, skip this section

         lsr     w1, w1      ;shift high word, LSB into C
         rrc     w0, w0      ;shift low word, C into MSB
         add     #1, w3      ;update the exponent to this extra 1 bit shift

flt48u_dround:               ;done rounding, final mantissa is in W1:W0
;
;   Merge in the exponent and sign fields to make the final floating point
;   number in W1:W0.
;
         ;
         ;   Range-check the exponent in W3.
         ;
         btsc    w3, #15     ;exponent didn't underflow ?
         jump    flt48u_zero ;underflow, return zero
         mov     #255, w4    ;get max possible exponent field value
         cp      w3, w4
         bra     gtu, flt48u_max ;overflow, return maximum possible value

         and     #0x7F, w1   ;clear the exponent and sign fields
         sl      w3, #7, w3  ;move the exponent field into position
         ior     w1, w3, w1  ;merge it into the floating point number

flt48u_leave:                ;floating point result is in W1:W0
         leaverest

flt48u_zero:                 ;return floating point 0
         mov     #0, w0
         mov     #0, w1
         jump    flt48u_leave

flt48u_max:                  ;return maximum possible value
         mov     #0x7FFF, w1
         mov     #0xFFFF, w0
         jump    flt48u_leave
  /endif
